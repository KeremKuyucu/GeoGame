plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    compileSdkVersion 35

    namespace = "com.keremkuyucu.geogame"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable true // ABI başına ayrı APK oluştur
            reset()     // Varsayılan ayarları sıfırla
            include 'armeabi-v7a', 'arm64-v8a', 'x86_64' // Desteklenecek mimariler
            universalApk true // Tek bir evrensel APK oluşturma
        }
    }
    defaultConfig {
        applicationId = "com.keremkuyucu.geogame"
        minSdkVersion flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }
    // Keystore configuration
    def keystorePropertiesFile = file("C:\\Users\\Kerem\\Projects\\Thinks\\key.properties")
    def keystoreProperties = new Properties()
    def hasValidKeystore = false
    
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        hasValidKeystore = keystoreProperties['storeFile'] != null && 
                          keystoreProperties['storeFile'] != ''
    }
    
    signingConfigs {
        if (hasValidKeystore) {
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    
    buildTypes {
        debug {
            signingConfig = signingConfigs.debug
        }
        release {
            if (hasValidKeystore) {
                signingConfig signingConfigs.release
            } else {
                // Fallback to debug signing when no keystore is configured
                signingConfig signingConfigs.debug
                println "WARNING: No valid keystore found. Using debug signing for release build."
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

flutter {
    source = "../.."
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

configurations.all {
    resolutionStrategy {
        force 'androidx.browser:browser:1.8.0'
        force 'androidx.core:core-ktx:1.13.1'
        force 'androidx.core:core:1.13.1'
    }
}